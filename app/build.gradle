apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "cz.eutopia.snooperstopper"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    // Automatically run ndk-build
    task ndkBuild(type: Exec) {
        workingDir file('src/main')
        commandLine 'ndk-build'
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

    task ndkClean(type: Exec) {
        workingDir file('src/main')
        commandLine 'ndk-build', 'clean'
    }

    clean.dependsOn ndkClean

    // Rename executables to lib*.so
    // (Android PackageManager only includes lib*.so files)
    task renameExecutables(type: Sync) {
        from('src/main/libs') {
            include '**/sepolicy-inject'
        }
        into('src/main/libs')
        rename '(.*)', 'lib$1.so'
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn renameExecutables
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
}
